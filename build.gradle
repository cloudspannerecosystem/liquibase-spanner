/**
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "groovy"
    id "com.google.cloud.tools.jib" version "3.3.1"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "com.github.harbby.gradle.serviceloader" version "1.1.5"
}


group = "com.google.cloudspannerecosystem"


if (project.hasProperty('tagVersion') && project.tagVersion.trim()) {
    project.version = project.tagVersion.trim()
} else {
   project.version = "SNAPSHOT"
}


repositories {
    mavenCentral()
    jcenter()
}


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// Liquibase <4 uses Liquibase-Package to determine what to scan
jar {
  manifest {
    attributes(
      "Liquibase-Package": "liquibase.ext.spanner"
    )
  }
}

// Liquibase 4 uses ServiceLoader
serviceLoader {
    serviceInterface 'liquibase.database.Database'
    serviceInterface 'liquibase.database.DatabaseConnection'
    serviceInterface 'liquibase.datatype.LiquibaseDataType'
    serviceInterface 'liquibase.sqlgenerator.SqlGenerator'
    serviceInterface 'liquibase.change.Change'
    serviceInterface 'liquibase.changelog.ChangeLogHistoryService'
    serviceInterface 'liquibase.snapshot.SnapshotGenerator'
}

dependencies {

    // Cloud Spanner related
    implementation platform('com.google.cloud:libraries-bom:26.3.0')
    implementation("com.google.cloud:google-cloud-spanner-jdbc")

    // Liquibase Core - needed for testing and docker container
    implementation("org.liquibase:liquibase-core:4.17.2")
    implementation("org.yaml:snakeyaml:1.33")
    implementation("org.apache.commons:commons-lang3:3.12.0")

    // Testing
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.1")
    testImplementation("org.testcontainers:testcontainers:1.17.6")
    testImplementation("net.java.dev.jna:jna:5.12.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.1")
    testImplementation("com.google.truth:truth:1.1.3")
    testImplementation("org.mockito:mockito-core:3.12.4")

    // For using the Liquibase test harness
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.liquibase:liquibase-test-harness:1.0.7'
    testImplementation('org.codehaus.groovy:groovy-all:2.5.20') {
        exclude module: 'org.codehaus.groovy:groovy-testng'
        exclude module: 'org.codehaus.groovy:groovy-swing'
    }
    testImplementation 'org.spockframework:spock-core:2.3-groovy-2.5'

    // For testing runtime
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.9.1")
    
    // For Spanner mock server testing
    testImplementation(group: 'com.google.cloud', name: 'google-cloud-spanner', classifier: 'tests')
    testImplementation(group: 'com.google.api', name: 'gax-grpc', version: '2.20.1', classifier: 'testlib')
}


// Run unit tests (Spanner emulator and mock tests)
test {

  // serviceLoaderBuild is necessary for Liquibase to find the extensions
  dependsOn "serviceLoaderBuild"

  useJUnitPlatform {
    excludeTags "integration"
  }
}

task liquibaseHarnessTest(type: Test) {

    // serviceLoaderBuild is necessary for Liquibase to find the extensions
    dependsOn "serviceLoaderBuild"

    // Use JUnit4 for Liquibase harness tests.
    useJUnit {
    }
}

// Run integration tests (against live Spanner)
//
// Requires SPANNER_PROJECT and SPANNER_INSTANCE environment variables.
// Also requires GOOGLE_APPLICATION_CREDENTIALS environment variable
// if using a JSON key for authentication.
task integrationTest(type: Test) {
  shouldRunAfter "test"

  // serviceLoaderBuild is necessary for Liquibase to find the extensions
  dependsOn "serviceLoaderBuild"

  useJUnitPlatform {
    includeTags "integration"
  }
}

build {

    // Build shadowJar during normal build
    // This includes all the needed classes for an extension
    dependsOn "shadowJar"
}

// Output shadowJar that can be used directly by Liquibase
//
// This contains all Cloud Spanner dependencies.
shadowJar {
    dependsOn "test"
    archiveBaseName.set("liquibase-spanner")
    mergeServiceFiles()
    dependencies {
        exclude([
          dependency("org.liquibase:liquibase-core")
        ])
    }
}

// Output runnable docker container.
//
// The docker container has everything you need to run Liquibase with Spanner.
jib {
    from {
        image = "gcr.io/distroless/java:8"
    }
    to {
        image = "liquibase-spanner"
    }
    container {
        mainClass = 'liquibase.integration.commandline.Main'
    }
}
